#!/usr/bin/env nu
# [MISE] description="More readable path. Duplicates in red. (Nushell version)"

let mise_prefix = $"($env.HOME)/.local/share/mise/installs"
let nix_prefix = "/nix/store"
let cryptex_prefix = "/var/run/com.apple.security.cryptexd"

let paths = $env.PATH

# Count occurrences of each non-placeholder path
let counts = $paths
    | where { |p| not ($p | str starts-with $mise_prefix) }
    | where { |p| not ($p | str starts-with $nix_prefix) }
    | where { |p| not ($p | str starts-with $cryptex_prefix) }
    | group-by { |p| $p }
    | items { |k v| { path: $k, count: ($v | length) } }
    | reduce -f {} { |it, acc| $acc | insert $it.path $it.count }

mut mise_shown = false
mut nix_shown = false
mut cryptex_shown = false

for p in $paths {
    if ($p | str starts-with $mise_prefix) {
        if not $mise_shown {
            print $"($mise_prefix)/..."
            $mise_shown = true
        }
    } else if ($p | str starts-with $nix_prefix) {
        if not $nix_shown {
            print $"($nix_prefix)/..."
            $nix_shown = true
        }
    } else if ($p | str starts-with $cryptex_prefix) {
        if not $cryptex_shown {
            print $"($cryptex_prefix)/..."
            $cryptex_shown = true
        }
    } else {
        let count = $counts | get -o $p | default 1
        if $count > 1 {
            print $"(ansi red)($p)(ansi reset)"
        } else {
            print $p
        }
    }
}
