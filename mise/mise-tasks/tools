#!/usr/bin/env nu
#MISE description="List miscellaneous tools"

def tools [] {
  [
    [group task desc];
    [pdf pdfattach "Attach PDFs"]
    [pdf pdfdetach "Detach PDFs"]
    [pdf pdftocairo "PDF to Cairo"]
    [pdf pdffonts "PDF fonts"]
    [pdf pdftohtml "PDF to HTML"]
    [pdf pdfimages "PDF to images"]
    [pdf pdfinfo "PDF info"]
    [pdf pdftoppm "PDF to ppm"]
    [pdf pdftops "PDF to ps"]
    [pdf pdfseparate "PDF separate"]
    [pdf pdfsig "PDF sig"]
    [pdf pdfunite "PDF unite"]
    [pdf pdftotext "PDF to text"]
    [dev zellij "Zellij terminal"]
    [dev tmux "tmux terminal"]
    [dev navi "Shell cheatsheet"]
    [dev lazyjj "lazyjj"]
    [dev lazysql "lazysql"]
    [dev lazydocker "lazydocker"]
    [util portal "file transfer (portal) tui"]
    [util glow "glow markdown viewer"]
    [json jq "jq json processing"]
    [json jqn "jq.node json processing"]
    [json yq "jq-like for yaml, json, xml, properties, csv and tsv"]
    [json jc "CLI output to json"]
    [json jid "json interactive digger"]
    [json fx "fx json viewer"]
    [json jnv "jnv json navigator"]
    [http hurl "curl test framework"]
    [http xh "curl alternative"]
    [http curlie "curl alternative"]
    [http posting "posting tui"]
    [dev sshs "ssh client tui"]
    [dev gitui "gitui git tui"]
    [dev tokei "count lines of code"]
    [dev biome "lint: biome check --write"]
    [dev lunchy "launch services ~/Library/LaunchAgents/"]
    [dev nerdlog "log tui"]
    [dev dblab "database tui"]
    [dev gobang "database tui"]
    [dev limactl "create linux vm"]
    [dev tursodb "turso db"]
    [dev delta "git delta viewer"]
    [dev hub "simplified github cli"]
    [dev caddy "caddy web server"]
    [dev sqlite3 "SQLite db cli"]
    [dev sqld "lib sql"]
    [dev shellcheck "shell linter"]
    [dev watchexec "watch for changes"]
    [unix ditto "copy alternative"]
    [unix sd "sed alternative for find/replace within files"]
    [unix sad "sed alternative with fzf"]
    [unix rg "ripgrep grep alternative"]
    [unix tldr "man alternative"]
    [unix bat "cat alternative"]
    [unix procs "ps alternative"]
    [unix btop "top tui"]
    [unix glances "top tui"]
    [unix macmon "resource monitor"]
    [unix tspin "tail alternative"]
    [unix choose "cut alternative"]
    [unix gping "ping alternative"]
    [util gpg-tui "GPG tui"]
    [util sniffnet "Network monitor"]
    [util lazyjournal "lazyjournal"]
    [util sk "skim, an fzf alternative"]
    [util tv "an fzf alternative"]
    [util spark "Sparkline generator"]
  ]
}

def mise_tasks [] {
  ^mise tasks ls
  | lines
  | each {|line|
      let parts = ($line | split row -r '\s{2,}')
      let name = ($parts | get 0)
      let desc = ($parts | get -o 1 | default "")
      if ($name | str contains ":") {
        let name_parts = ($name | split row ":")
        {group: ($name_parts | get 0) task: ($name_parts | get 1) desc: $desc}
      } else {
        {group: "misc" task: $name desc: $desc}
      }
    }
}

def format_row [row] {
  let task = ($row.task | fill -w 22)
  $"($task) (ansi grey)($row.desc)(ansi reset)"
}

def main [filter?: string] {
  let all_data = (mise_tasks | append (tools))

  let filtered = if $filter == null {
    $all_data
  } else {
    $all_data | where group == $filter
  }

  if ($filtered | is-empty) {
    return
  }

  let groups = ($filtered | get group | uniq | sort)

  $groups
  | each {|grp|
      let rows = ($filtered | where group == $grp | sort-by task)
      let header = $"(ansi green)($grp | str upcase)(ansi reset)"
      let items = ($rows | each {|r| format_row $r} | str join "\n")
      $"($header)\n($items)"
    }
  | str join "\n\n"
}
