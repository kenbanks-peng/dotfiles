#!/usr/bin/env nu
#MISE description="List miscellaneous tools"

def uv_tools [] {
  try {
    ^uv tool ls
    | lines
    | where {|line| ($line | str starts-with "-") == false and ($line | str trim | is-not-empty)}
    | each {|line|
        let parts = ($line | split row " ")
        let name = ($parts | get 0)
        {group: "uv" command: $name desc: "uv"}
      }
  } catch {
    []
  }
}

def bun_tools [] {
  let result = (do { ^bun pm ls -g } | complete)
  if $result.exit_code == 0 {
    $result.stdout
    | lines
    | skip 1
    | each {|line|
        let clean = ($line | str replace -r '^[└├─│ ]+' '')
        let name = ($clean | split row "@" | get 0)
        {group: "bun" command: $name desc: "bun"}
      }
  } else {
    []
  }
}

def brew_tools [] {
  let result = (do { ^brew leaves --installed-on-request } | complete)
  if $result.exit_code == 0 {
    $result.stdout
    | lines
    | where {|line| $line | str trim | is-not-empty}
    | each {|line|
        # Extract just the formula name from tap paths like "tako8ki/tap/gobang"
        let name = if ($line | str contains "/") {
          $line | split row "/" | last
        } else {
          $line
        }
        {group: "brew" command: $name desc: "brew"}
      }
  } else {
    []
  }
}

def mise_list [] {
  let result = (do { ^mise list } | complete)
  if $result.exit_code == 0 {
    $result.stdout
    | lines
    | where {|line| $line | str trim | is-not-empty}
    | each {|line|
        let parts = ($line | split row -r '\s+')
        let full = ($parts | get 0)
        # Extract backend and package from "backend:package"
        let backend = ($full | split row ":" | get 0)
        let package = ($full | split row ":" | skip 1 | str join ":")
        # Extract command name (last segment after / or the whole package)
        let name = if ($package | str contains "/") {
          $package | split row "/" | last
        } else {
          $package
        }
        {group: "mise" command: $name desc: $backend}
      }
  } else {
    []
  }
}

def tools_metadata [] {
  let script_dir = ($env.FILE_PWD? | default (pwd))
  open ($script_dir | path join "tools.nuon")
}

def mise_tasks [] {
  ^mise tasks ls
  | lines
  | each {|line|
      let parts = ($line | split row -r '\s{2,}')
      let name = ($parts | get 0)
      let desc = ($parts | get -o 1 | default "")
      if ($name | str contains ":") {
        let name_parts = ($name | split row ":")
        {group: ($name_parts | get 0) command: ($name_parts | get 1) desc: $desc}
      } else {
        {group: "misc" command: $name desc: $desc}
      }
    }
}

def format_row [row] {
  let command = ($row.command | fill -w 22)
  $"($command) (ansi grey)($row.desc)(ansi reset)"
}

def main [filter?: string] {
  let metadata = (tools_metadata)
  let uv = (uv_tools)
  let bun = (bun_tools)
  let brew = (brew_tools)
  let mise_installed = (mise_list)
  let mise = (mise_tasks)

  # Combine all available tools with their source
  let available = ($uv | each {|t| {command: $t.command source: "uv"}}
    | append ($bun | each {|t| {command: $t.command source: "bun"}})
    | append ($brew | each {|t| {command: $t.command source: "brew"}})
    | append ($mise_installed | each {|t| {command: $t.command source: "mise" desc: $t.desc}})
    | append ($mise | each {|t| {command: $t.command source: "mise" group: $t.group desc: $t.desc}}))

  # Deduplicate by command name, preferring mise tasks (which have group/desc)
  let unique_commands = ($available | group-by command | transpose name tools | each {|t|
      let mise_tools = ($t.tools | where source == "mise")
      if ($mise_tools | is-not-empty) {
        $mise_tools | first
      } else {
        $t.tools | first
      }
    })

  # Enrich with metadata from tools.nuon
  # Match by name (cli) if present, otherwise by command (display name)
  # A single package can expand to multiple commands (e.g., poppler -> pdfattach, pdfdetach, etc.)
  let enriched = ($unique_commands | each {|t|
      let meta_matches = ($metadata | where {|m| ($m.name? | default $m.command) == $t.command})
      if ($meta_matches | is-not-empty) {
        $meta_matches | each {|meta| {group: $meta.group command: $meta.command desc: $meta.desc}}
      } else if ($t.group? != null) {
        [{group: $t.group command: $t.command desc: ($t.desc? | default "")}]
      } else {
        [{group: $t.source command: $t.command desc: $t.source}]
      }
    } | flatten)

  let filtered = if $filter == null {
    $enriched
  } else {
    $enriched | where group == $filter
  }

  if ($filtered | is-empty) {
    return
  }

  let all_groups = ($filtered | get group | uniq)
  let main_groups = ($all_groups | where {|g| $g != "bun" and $g != "uv" and $g != "brew" and $g != "mise"} | sort)
  let end_groups = ($all_groups | where {|g| $g == "bun" or $g == "uv" or $g == "brew" or $g == "mise"})
  let groups = ($main_groups | append $end_groups)

  $groups
  | each {|grp|
      let rows = ($filtered | where group == $grp | sort-by command)
      let header = $"(ansi green)($grp | str upcase)(ansi reset)"
      let items = ($rows | each {|r| format_row $r} | str join "\n")
      $"($header)\n($items)"
    }
  | str join "\n\n"
  | $in + "\n"
}
