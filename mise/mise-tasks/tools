#!/usr/bin/env nu
#MISE description="List miscellaneous tools"

def uv_tools [] {
  try {
    ^uv tool ls
    | lines
    | where {|line| ($line | str starts-with "-") == false and ($line | str trim | is-not-empty)}
    | each {|line|
        let parts = ($line | split row " ")
        let name = ($parts | get 0)
        {group: "uv" task: $name desc: "uv"}
      }
  } catch {
    []
  }
}

def bun_tools [] {
  let result = (do { ^bun pm ls -g } | complete)
  if $result.exit_code == 0 {
    $result.stdout
    | lines
    | skip 1
    | each {|line|
        let clean = ($line | str replace -r '^[└├─│ ]+' '')
        let name = ($clean | split row "@" | get 0)
        {group: "bun" task: $name desc: "bun"}
      }
  } else {
    []
  }
}

def tools [] {
  let script_dir = ($env.FILE_PWD? | default (pwd))
  open ($script_dir | path join "tools.csv")
}

def mise_tasks [] {
  ^mise tasks ls
  | lines
  | each {|line|
      let parts = ($line | split row -r '\s{2,}')
      let name = ($parts | get 0)
      let desc = ($parts | get -o 1 | default "")
      if ($name | str contains ":") {
        let name_parts = ($name | split row ":")
        {group: ($name_parts | get 0) task: ($name_parts | get 1) desc: $desc}
      } else {
        {group: "misc" task: $name desc: $desc}
      }
    }
}

def format_row [row] {
  let task = ($row.task | fill -w 22)
  $"($task) (ansi grey)($row.desc)(ansi reset)"
}

def main [filter?: string] {
  let base_tools = (tools)
  let base_names = ($base_tools | get task)
  let filtered_uv = (uv_tools | where {|t| $t.task not-in $base_names})
  let filtered_bun = (bun_tools | where {|t| $t.task not-in $base_names})
  let all_data = (mise_tasks | append $base_tools | append $filtered_uv | append $filtered_bun)

  let filtered = if $filter == null {
    $all_data
  } else {
    $all_data | where group == $filter
  }

  if ($filtered | is-empty) {
    return
  }

  let all_groups = ($filtered | get group | uniq)
  let main_groups = ($all_groups | where {|g| $g != "bun" and $g != "uv"} | sort)
  let end_groups = ($all_groups | where {|g| $g == "bun" or $g == "uv"})
  let groups = ($main_groups | append $end_groups)

  $groups
  | each {|grp|
      let rows = ($filtered | where group == $grp | sort-by task)
      let header = $"(ansi green)($grp | str upcase)(ansi reset)"
      let items = ($rows | each {|r| format_row $r} | str join "\n")
      $"($header)\n($items)"
    }
  | str join "\n\n"
  | $in + "\n"
}
